plugins {
    id 'org.quiltmc.loom' version '0.12.+'
}

archivesBaseName = rootProject.name
version = "v${modVersion}-${minecraftVersion}-Common"
group = modMavenGroup

dependencies {
    // Minecraft
    minecraft "com.mojang:minecraft:${minecraftVersion}"
    mappings loom.layered() {
        officialMojangMappings()
//        parchment("org.parchmentmc.data:parchment-${minecraftVersion}:${parchmentMappingsVersion}@zip")
    }

    // ASM dependencies do not seem to be applying correctly without this for Mixins (manually adding those didn't help). Do not use anything from this!
    modImplementation "org.quiltmc:quilt-loader:${quiltVersion}"

    // Common Dependencies
    compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
    compileOnly 'com.electronwill.night-config:core:3.6.5'
    compileOnly 'com.electronwill.night-config:toml:3.6.5'

    // Puzzles Lib
//    modImplementation "fuzs.puzzleslib:puzzleslib-common:${puzzlesVersion}-Common"
}

processResources {
    duplicatesStrategy DuplicatesStrategy.INCLUDE

    // this will ensure that this task is redone when a value changes
    inputs.property "modDescription", 			"${modDescription}"
    inputs.property "packFormat", 				"${packFormat}"

    // replace stuff in pack.mcmeta
    filesMatching ('pack.mcmeta') {
        expand (
                'modDescription':       "${modDescription}",
                "packFormat":           "${packFormat}"
        )
    }
}

publishing {
    publications {
        mavenJava (MavenPublication) {
            artifactId = "${modId}-common"
            version = modVersion
            from components.java
            // exclude certain dependencies when publishing to maven
            // from https://stackoverflow.com/a/50121790
            pom.withXml {
                asNode().dependencies.dependency.each { dep ->
                    if ([].stream().anyMatch(mod -> dep.artifactId.last().value().last().contains(mod))) {
                        assert dep.parent().remove(dep)
                    }
                }
            }
            pom {
                name = "${modName} [Common]"
                description = "${modDescription}"
                url = "${modSourceUrl}"
                scm {
                    url = "${modSourceUrl}"
                    connection = "${modSourceUrl}".replace("https", "scm:git:git").concat(".git")
                    developerConnection = "${modSourceUrl}".replace("https://github.com/", "scm:git:git@github.com:").concat(".git")
                }
                issueManagement {
                    system = 'github'
                    url = "${modIssueUrl}"
                }
                licenses {
                    license {
                        name = 'MPL-2'
                        url = 'https://www.mozilla.org/en-US/MPL/2.0/'
                    }
                }
                developers {
                    developer {
                        id = "${modAuthor}".toLowerCase()
                        name = "${modAuthor}"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = 'FuzsModResources'
            url "file://${project.modResources}/maven"
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
